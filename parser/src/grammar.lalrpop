use super::lexer;
use super::ast;
use errors;

use super::ast::Spanned as Sp;

grammar<>;
extern {
    type Location = usize;
    type Error = errors::LexicalError;

    enum lexer::Token<> {
        "ident" => lexer::Token::Identifier(<String>),
        "string" => lexer::Token::StringLiteral(<String>),
        "int" => lexer::Token::IntLiteral(<i64>),
        "float" => lexer::Token::FloatLiteral(<f64>),
        "return" => lexer::Token::Return,
    
        "and" => lexer::Token::And,
        "or" => lexer::Token::Or,
        "if" => lexer::Token::If,
        "else" => lexer::Token::Else,
        "for" => lexer::Token::For,
        "true" => lexer::Token::True,
        "false" => lexer::Token::False,
        "let" => lexer::Token::Let,
        "gpu_fun" => lexer::Token::GpuFun,
        "pipeline" => lexer::Token::Pipeline,
        
	    "(" => lexer::Token::ParOpen,
        ")" => lexer::Token::ParClose,
        ":" => lexer::Token::Colon,
        ";" => lexer::Token::Semicolon,
        "/" => lexer::Token::Slash,
        "," => lexer::Token::Comma,
        "." => lexer::Token::Dot,
        "{" => lexer::Token::BraceOpen,
        "}" => lexer::Token::BraceClose,
        "[" => lexer::Token::BracketOpen,
        "]" => lexer::Token::BracketClose,
        "?" => lexer::Token::Question,
        "!" => lexer::Token::ExclMark,
        "*" => lexer::Token::Star,
        "+" => lexer::Token::Plus,
        "-" => lexer::Token::Minus,
        "!=" => lexer::Token::NotEqual,
        "==" => lexer::Token::VeryEqual,
        "=" => lexer::Token::Equal,        //
        ">" => lexer::Token::Greater,      //
        ">=" => lexer::Token::GreaterEqual, //
        "<" => lexer::Token::Less,         //
        "<=" => lexer::Token::LessEqual,    //
        "->" => lexer::Token::FunResultsArrow,
    }
    
}

// Comma separated 
Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

pub Program: ast::Program = {
    <items: (GpuFunction)*> <pipeline: Pipeline>=> ast::Program{items, pipeline}
}

pub Pipeline: ast::Pipeline = {
    "pipeline" <l:@L> <i:"ident"> <r:@R> "(" <args:Comma<Definition>> ")"
        "->" "(" <results:Comma<Type>> ")" <b: Block> =>
            ast::Pipeline{
                name: Sp::from_loc(i, l,r),
                arguments: args,
                results,
                block: b
            }
}

pub GpuFunction: ast::GpuFunction = {
    "gpu_fun" <l:@L> <i:"ident"> <r:@R> "(" <args:Comma<Definition>> ")" "->" "(" <results:Comma<Definition>> ")" "{" <l2:@L> <s:"string"> <r2:@R> "}" => ast::GpuFunction{ name: Sp::from_loc(i, l,r), code: Sp::from_loc(s, l2, r2), arguments: args, results}
}

pub Block: ast::Block = {
    "{" <statements: (<Statement> ";")*> "}" => ast::Block{statements: statements },
}

pub Statement: ast::Statement = {
    <lr:"let"?> <l:@L> <id:"ident"> <r:@R> "=" <e:Expression> => ast::Statement::Assignment(Box::new(ast::Variable::new(Sp::from_loc(id, l, r))), e, match lr {Some(_)=> true, None =>false,}),
    "return" <l:@L> <e:Expression> <r:@R> => ast::Statement::Return(e),
}
pub Expression: Box<ast::Expression> = {
    <l:@L> <id:"ident"> <r:@R> => Box::new(ast::Expression::Variable(ast::Variable::new(Sp::from_loc(id, l, r)))),
    "(" <e:Expression> ")" => e,
    <i:"ident"> "("<args: Comma<Expression>> ")" => Box::new(ast::Expression::Invocation(i, args)),
}

Type: ast::Spanned<ast::Type> = {
    <l:@L> <il:"ident"> <r:@R> => Sp::from_loc(ast::Type::new(il), l, r)
}

Definition: ast::Variable = {
    <l:@L> <i1:"ident"> <r:@R> ":" <i2:"ident"> => ast::Variable::typed(Sp::from_loc(i1, l,r), ast::Type::new(i2))
}

Literal: ast::Literal = {
    <l:@L> <val:"int"> <r:@R>=> ast::Literal::Int(Sp::from_loc(val, l,r)),
    <l:@L> <val:"float"> <r:@R> => ast::Literal::Float(Sp::from_loc(val, l,r)),
    <l:@L> <val:"string"> <r:@R> => ast::Literal::String(Sp::from_loc(val, l,r)),

}
