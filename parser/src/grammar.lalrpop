use super::lexer;
use super::ast;
use errors;

grammar<>;
extern {
    type Location = usize;
    type Error = errors::LexicalError;

    enum lexer::Token<> {
        "ident" => lexer::Token::Identifier(<String>),
        "string" => lexer::Token::StringLiteral(<String>),
        "int" => lexer::Token::IntLiteral(<i64>),
        "float" => lexer::Token::FloatLiteral(<f64>),
    
        "and" => lexer::Token::And,
        "or" => lexer::Token::Or,
        "if" => lexer::Token::If,
        "else" => lexer::Token::Else,
        "for" => lexer::Token::For,
        "true" => lexer::Token::True,
        "false" => lexer::Token::False,
        "let" => lexer::Token::Let,
        "fun" => lexer::Token::Fun,
        
	    "(" => lexer::Token::ParOpen,
        ")" => lexer::Token::ParClose,
        ":" => lexer::Token::Colon,
        ";" => lexer::Token::Semicolon,
        "/" => lexer::Token::Slash,
        "," => lexer::Token::Comma,
        "." => lexer::Token::Dot,
        "{" => lexer::Token::BraceOpen,
        "}" => lexer::Token::BraceClose,
        "[" => lexer::Token::BracketOpen,
        "]" => lexer::Token::BracketClose,
        "?" => lexer::Token::Question,
        "!" => lexer::Token::ExclMark,
        "*" => lexer::Token::Star,
        "+" => lexer::Token::Plus,
        "-" => lexer::Token::Minus,
        "!=" => lexer::Token::NotEqual,
        "==" => lexer::Token::VeryEqual,
        "=" => lexer::Token::Equal,        //
        ">" => lexer::Token::Greater,      //
        ">=" => lexer::Token::GreaterEqual, //
        "<" => lexer::Token::Less,         //
        "<=" => lexer::Token::LessEqual,    //
    }
    
}

// Comma separated 
Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

pub Program: ast::Program = {
    <items: (ProgramItem)*> => ast::Program{items}
}


ProgramItem: ast::ProgramItem = {
    Function => ast::ProgramItem::Function(Box::new(<>)),
    GpuFunction => ast::ProgramItem::GpuFunction(Box::new(<>)),

}

pub Function: ast::Function = {
    "fun" <i:"ident"> "(" <args:Comma<Definition>> ")"  <ri:"ident"?> <b:Block> => ast::Function{arguments: args, name: i, block: b, ret: ri.map_or(None, |x| Some(ast::Type::new(x)))}
}

pub GpuFunction: ast::GpuFunction = {
    "fun" <i:"ident"> "(" <args:Comma<Definition>> ")" "{" <s:"string"> "}" => ast::GpuFunction{ name:i, code: s, arguments: args}
}

Definition: (ast::Variable, ast::Type) = {
    <i1:"ident"> ":" <i2:"ident"> => (ast::Variable::new(i1), ast::Type::new(i2))
}

pub Block: ast::Block = {
    "{" <statements: (<Statement> ";")*> "}" => ast::Block{statements: statements },
}

pub Statement: ast::Statement = {
    Expression => ast::Statement::Expression(<>),
    <l:"let"?> <i:"ident"> "=" <e:Expression> => ast::Statement::Assignment(Box::new(ast::Variable::new(i)), e, match l {Some(_)=> true, None =>false,}),
}



pub Expression: Box<ast::Expression> = {
    Equaled => <>,
    <e1:Expression> "and" <e2:Equaled> => Box::new(ast::Expression::And(e1, e2)),
    <e1:Expression> "or" <e2:Equaled> => Box::new(ast::Expression::Or(e1, e2))
}

Equaled: Box<ast::Expression> = {
    Related => <>,
    <a1:Added> "==" <a2:Added> =>  Box::new(ast::Expression::Equals(a1, a2)),
    <a1:Added> "!=" <a2:Added> =>  Box::new(ast::Expression::NotEquals(a1, a2)),
}

Related: Box<ast::Expression> = {
    Added => <>,
    <r:Related> "<" <a:Added> => Box::new(ast::Expression::Less(r, a)),
    <r:Related> "<=" <a:Added> => Box::new(ast::Expression::LessEqual(r, a)),
    <r:Related> ">" <a:Added> => Box::new(ast::Expression::More(r, a)),
    <r:Related> ">=" <a:Added> => Box::new(ast::Expression::MoreEqual(r, a)),
}

Added: Box<ast::Expression> = {
    Multiplied => <>,
    <a:Added> "+" <m:Multiplied> => Box::new(ast::Expression::Add(a, m)),
    <a:Added> "-" <m:Multiplied> => Box::new(ast::Expression::Sub(a, m)),
}

Multiplied: Box<ast::Expression> = {
    Negated => <>,
    <m:Multiplied> "*" <n:Negated> => Box::new(ast::Expression::Mul(m, n)),
    <m:Multiplied> "/" <n:Negated> => Box::new(ast::Expression::Div(m, n)),
}
Negated: Box<ast::Expression> = {
    "!" <p:Primitive> => Box::new(ast::Expression::Negation(p)),
    Primitive => <> 
}

pub Primitive: Box<ast::Expression> = {
    Literal => Box::new(ast::Expression::Literal(<>)),
    "ident" => Box::new(ast::Expression::Variable(ast::Variable::new(<>))),
    "(" <e:Expression> ")" => e,
    <i:"ident"> "("<args: Comma<Expression>> ")" => Box::new(ast::Expression::Invocation(i, args)),
}

Literal: ast::Literal = {
    "int" => ast::Literal::Int(<>),
    "float" => ast::Literal::Float(<>),
    "string" => ast::Literal::String(<>),

}
